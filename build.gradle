apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

ext.home = System.env.HOME
ext.ideaPath = "/Users/dima/Library/Application Support/JetBrains/Toolbox/apps/IDEA-U/ch-0/172.3198.6/IntelliJ IDEA 2017.2 EAP.app/Contents"
ext.pluginsSandbox = "${home}/Library/Caches/IntelliJIdea2016.3/plugins-sandbox"

repositories { mavenCentral() }
enableProvidedScope()
generateModuleFileConfiguredForPluginDevelopment()

dependencies {
	provided group: 'junit', name: 'junit', version: '4.10' // also includes hamcrest-core
	provided group: 'org.jetbrains.kotlin', name: 'kotlin-compiler-embeddable', version: '1.1.2'
	provided group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.1.2'
	provided group: 'org.clojure', name: 'clojure', version: '1.7.0'
	provided group: 'org.scala-lang', name: 'scala-compiler', version: '2.11.7'
	// also includes scala-parser-combinators, scala-xml
	provided group: 'org.scala-lang', name: 'scala-library', version: '2.11.7'
	provided group: 'org.scala-lang', name: 'scala-reflect', version: '2.11.7'
	provided fileTree(dir: "${ideaPath}/lib/", include: "groovy-all-*.jar")
	provided fileTree(dir: "${ideaPath}/lib/", include: "gson-*.jar") // required by github plugin classes
	provided fileTree(dir: "${ideaPath}/lib/", include: "xstream-*.jar")
	provided fileTree(dir: "${ideaPath}/plugins/github/lib/", include: "*.jar")
	provided fileTree(dir: "${ideaPath}/plugins/git4idea/lib/", include: "*.jar")
	provided fileTree(dir: "${ideaPath}/plugins/junit/lib/", include: "*.jar")
	provided fileTree(dir: "${ideaPath}/plugins/Kotlin/lib/", include: "kotlin-plugin.jar")
}
sourceSets {
	main {
		java { srcDir "src" }
		resources { srcDir "resources" }
		groovy { srcDir "src_groovy" }
		groovy { srcDir "plugin_examples" }
	}
	test {
		groovy { srcDir "test_groovy" }
	}
}

// Need this task to be executed before plugin run configuration
// because for some reason IntelliJ doesn't reliably copy resources with current project configuration
task copyResources { doLast {
	def classesFolder = "${pluginsSandbox}/plugins/LivePlugin/classes"
	println("Copying resources to ${classesFolder}")

	ant.copy(toDir: classesFolder, overwrite: true) {
		ant.fileset(dir: "./plugin_examples", includes: "**/*")
	}
	ant.copy(toDir: classesFolder, overwrite: true) {
		ant.fileset(dir: "./resources", includes: "**/*")
	}
	ant.copy(toDir: classesFolder + "/src", overwrite: true) {
		ant.fileset(dir: "./src_groovy", includes: "**/*")
	}
} }

task downloadDependenciesToLibFolder(type: Copy) {
	from configurations.runtime
	into './lib'
}

task installLivePlugin() { doLast {
	def version = new File("resources/META-INF/plugin.xml").readLines()
			.find { it.contains("<version>") }
			.replaceFirst(".*<version>", "")
			.replaceFirst("</version>.*", "")
	def localMavenPath = repositories.mavenLocal().url.path + "/liveplugin/live-plugin/"
	def toFile = "${localMavenPath}/${version}/live-plugin-${version}.jar"

	ant.copy(
		file: "LivePlugin.jar",
		toFile: toFile
	)

	println("Installed to local maven liveplugin version " + version)
	println("Path: ${toFile}")
} }


def enableProvidedScope() {
	configurations {
		provided
		compile.extendsFrom provided
	}
	idea {
		module { scopes.PROVIDED.plus += [configurations.provided] }
	}
}

// see http://blog.xebia.com/building-intellij-plugins-from-the-command-line/
def generateModuleFileConfiguredForPluginDevelopment() {
	idea {
		module {
			iml {
				withXml {
					it.asNode().@type = 'PLUGIN_MODULE'
					//  setting url for <component name="DevKit.ModuleBuildProperties" ... />
					def cmp = it.node.appendNode('component')
					cmp.@name = 'DevKit.ModuleBuildProperties'
					cmp.@url = 'file://$MODULE_DIR$/resources/META-INF/plugin.xml'
				}
			}
		}
	}
}
